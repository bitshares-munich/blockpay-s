apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

android {
    signingConfigs {
        debug {
        }
    }
    compileSdkVersion 23
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "de.bitsharesmunich.blockpay"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 17
        versionName '1.5.13'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
            signingConfig signingConfigs.debug
        }
        preRelease {
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation'
        checkReleaseBuilds false
        abortOnError false
    }
    android.packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':aFileDialog')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile('com.github.bilthon:graphenej:0.4.3-SNAPSHOT') { changing = true }
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.github.rey5137:material:1.2.2'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.github.dmytrodanylyk.shadow-layout:library:1.0.3'
    compile 'com.itextpdf:itextpdf:5.5.9'
    compile 'de.codecrafters.tableview:tableview:2.0.1'
    compile 'cz.msebera.android:httpclient:4.4.1.2'
    compile 'com.koushikdutta.async:androidasync:2.0.0'
    compile 'org.bitcoinj:bitcoinj-core:0.14.3'
    compile 'com.neovisionaries:nv-websocket-client:1.30'
    compile 'org.tukaani:xz:1.6'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.opencsv:opencsv:3.7'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}

afterEvaluate {
    initFabricPropertiesIfNeeded()
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsApiSecret)
            entry(key: "apiKey", value: crashlyticsApiKey)
        }
    }
}
